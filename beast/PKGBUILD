# Maintainer: speps <speps at aur dot archlinux dot org>
# Contributor: Corrado 'bardo' Primier <corrado.primiermail.polimi.it>
# Contributor: Gerardo Exequiel Pozzi <vmlinuz386@yahoo.com.ar>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=beast
pkgver=0.8.2
pkgrel=1
pkgdesc="A music composition and modular synthesis application"
arch=('i686' 'x86_64')
url="http://beast.testbit.eu/"
license=('GPL' 'LGPL')
depends=('alsa-lib' 'flac' 'guile1.8' 'libgnomecanvas' 'libmad'
         'libvorbis' 'rapicorn' 'desktop-file-utils')
provides=("bse-alsa=$pkgname")
conflicts=('bse-alsa')
options=('!libtool')
install="$pkgname.install"
source=("http://dist.testbit.eu/beast/v${pkgver%.*}/beast-$pkgver.tar.bz2"
        "https://git.gnome.org/browse/beast/plain/bse/bseapi.idl?id=0.8.2")
md5sums=('8d8a1c005e2be0370d3047ff2732e179'
         '1aa5421aca7ec851b6817b68777e3a9a')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # use guile 1.8
  sed -i 's/guile-config/&1.8/' configure

  # missing file
  cp ../${source[1]##*/} bse/bseapi.idl

  # strip deprecated glib2 stuff
  sed -i '/g_thread_init/d' `grep -rl g_thread_init`
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install

  # rm mime cache
  find "$pkgdir/usr/share/mime" \
    -maxdepth 1 -type f -exec rm {} \;
}
