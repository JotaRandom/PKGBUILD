# Maintainer: Sven Schneider <archlinux.sandmann@googlemail.com>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>

pkgname=eclipse-mylyn
pkgver=3.14.2
_reldate=20150218-1814
pkgrel=1
pkgdesc="A task-focused interface for Eclipse"
arch=('any')
url="http://www.eclipse.org/mylyn/"
license=('EPL')
depends=('eclipse')
makedepends=('unzip' 'java-environment')
optdepends=('bugzilla: ticketing support')
source=(http://download.eclipse.org/mylyn/drops/${pkgver}/v${_reldate}/mylyn-${pkgver}.v${_reldate}.zip)
replaces=('eclipse-mylyn-extras')
changelog=ChangeLog
provides=("eclipse-mylyn-extras=${pkgver}")
md5sums=('b9d596ffad33268dded3442e126559d0')
sha256sums=('ee08bfb8dbb49e7c0aadc62f28363002642cd30b06d2b7f45cdcbfbbcbd1e3a6')
sha384sums=('9360ada7add0e6970fea4db90e87d12a7466a1de55b6497c7476ef1c5d83b6ff5190555c80e49cf4dd6a835e4f8de6b3')
sha512sums=('0689f897c2868ee1c7b413033577d398318765f1e473a4c3665901f398e82fd6ef468441d1a139fe5ab4e83adbe3d9b73a653f0db8132ad1f778f0a900168ab2')

package() {
  _dest=${pkgdir}/usr/share/eclipse/dropins/${pkgname/eclipse-}/eclipse

  cd ${srcdir}

  # Features
  find features -type f | while read _feature ; do
    if [[ ${_feature} =~ (.*\.jar$) ]] ; then
      install -dm755 ${_dest}/${_feature%*.jar}
      cd ${_dest}/${_feature/.jar}
      jar xf ${srcdir}/${_feature} || return 1
      cd - >/dev/null 2>&1
    elif [[ ! ${_feature} =~ (.*\.jar.pack.gz) ]] || [ ! -e ${_feature%\.pack\.gz$} ] ; then
      install -Dm644 ${_feature} ${_dest}/${_feature}
    fi
  done

  # Plugins
  find plugins -type f | while read _plugin ; do
    if [[ ! ${_plugin} =~ (.*\.jar.pack.gz) ]] || [ ! -e ${_plugin%\.pack\.gz$} ] ; then
      install -Dm644 ${_plugin} ${_dest}/${_plugin}
    fi
  done
}
