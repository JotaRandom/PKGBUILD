pkgname=obconf-qt-git
_gitname=obconf-qt
pkgver=b97a5d8
pkgrel=1
pkgdesc="Qt port of openbox configuration tool ObConf"
arch=('i686' 'x86_64')
url="http://lxde.git.sourceforge.net"
license=('GPL')
depends=('openbox' 'qt4' 'desktop-file-utils')
makedepends=('git' 'cmake')
install=${pkgname}.install
source=("obconf-qt::git://lxde.git.sourceforge.net/gitroot/lxde/obconf-qt")
md5sums=('SKIP')

pkgver() {
  cd $_gitname
  git describe --always | sed 's|-|.|g'
}

build() {
  cd "${srcdir}"/${_gitname}
  cmake . -DCMAKE_BUILD_TYPE=Release \
          -DCMAKE_INSTALL_PREFIX=/usr \
          -DQT_QMAKE_EXECUTABLE=qmake-qt4
  make
}

package() {
  cd "${srcdir}"/${_gitname}
  make DESTDIR="${pkgdir}" install
}
