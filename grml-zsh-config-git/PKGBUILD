# Maintainer: Kevin Dodd <Jesin00@gmail.com>
# Contributor: Pablo Lezaeta <prflr@gmail.com>

pkgname=grml-zsh-config-git
_pkgname=grml-etc-core
pkgver=0.9.1.r1069
pkgrel=1
pkgdesc="grml's zsh setup - from git repositories"
arch=('any')
url='http://grml.org/zsh/'
license=('GPL2')
depends=('zsh' 'coreutils' 'inetutils' 'grep' 'sed' 'procps')
makedepends=('git' 'txt2tags')
conflicts=('grml-zsh-config')
provides=('grml-zsh-config' 'grmlzshrc')
source=("${_pkgname}::git+https://github.com/grml/grml-etc-core.git")
md5sums=('SKIP')

pkgver(){
  cd "${_pkgname}"
  echo $(git describe --always --abbrev=0 | sed 's|v||g').r$(git rev-list --count HEAD | sed 's/-/+/g' )
}

build() {
  cd "${srcdir}/${_pkgname}/doc"
  make
}

package() {
	cd "${srcdir}/${_pkgname}"
	install -D -m644 etc/skel/.zshrc "${pkgdir}/etc/skel/.zshrc"
	install -D -m644 etc/zsh/keephack "${pkgdir}/etc/zsh/keephack"
	install -D -m644 etc/zsh/zshrc "${pkgdir}/etc/zsh/zshrc"

	install -D -m644 doc/grmlzshrc.5 "${pkgdir}/usr/share/man/man5/grmlzshrc.5"
	ln -sf grmlzshrc.5.gz "${pkgdir}/usr/share/man/man5/grml-zsh-config.5.gz"
}
