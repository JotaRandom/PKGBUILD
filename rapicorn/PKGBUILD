# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=rapicorn
pkgver=13.07.0
pkgrel=1
pkgdesc="A toolkit for rapid development of graphical user interfaces using C++ and Python"
arch=(i686 x86_64)
url="http://rapicorn.testbit.eu/"
license=('LGPL')
depends=('python2' 'libpng12' 'pango')
options=('!libtool')
source=("http://dist.testbit.eu/rapicorn/rapicorn-$pkgver.tar.bz2")
md5sums=('d19b00d5970d42f299744e4abf910e05')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # use python 2.7
  export PYTHON=python2 PYTHON_VERSION=2.7

  # python2 shebang
  sed -i "s/env python$/&2/" `find . -name "*.py"`

  # use libpng12
  sed -i "s|png.h|libpng12/&|" ui/pixmap.cc

  # DSO link fix + missing librt
  export LDFLAGS+=" -lcairo -lrt"
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
