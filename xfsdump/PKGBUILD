# Contributor: judd <jvinet@zeroflux.org>
# Maintainer: dkaylor <dpkaylor@gmail.com>

pkgname=xfsdump
pkgver=3.1.4
pkgrel=1
pkgdesc="Additional XFS filesystem utilities"
arch=(i686 x86_64)
url="http://oss.sgi.com/projects/xfs/"
license=('LGPL')
depends=('xfsprogs>=3.1.0' 'attr>=2.4.43' 'acl>=2.2.47' 'dmapi>=2.2.10')
options=('!makeflags' '!emptydirs' '!libtool')
source=(ftp://oss.sgi.com/projects/xfs/cmd_tars/$pkgname-$pkgver.tar.gz{,.sig})

md5sums=('a8b1761be5feb363131e7b506639ad4c'
         'SKIP')
sha1sums=('3c06e0e5c32ce09ebc4c0e903fb6105c53c8177c'
          'SKIP')
sha256sums=('570eafd0721515bdd79cb0e295b701d49cdf81e71a0a0ff0df6d4c5cc1960943'
            'SKIP')
sha512sums=('e82885ad6b3f1d81c1cdeff56714b73651e1c74eb4ce71574875ec0bb69b3fe707b40f85f0766b21ba589d13364ecfc0bad657a9b15c428cf821f926c3d66b25'
            'SKIP')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DIST_ROOT="${pkgdir}" install

  # hack to merge binaries from /sbin and /usr/sbin into /usr/bin #
  mkdir -p $pkgdir/usr/bin
  mv $pkgdir/{sbin/*,usr/sbin/xfsinvutil} $pkgdir/usr/bin
  rm -rf $pkgdir/{sbin,usr/sbin}

  chown -R root $pkgdir
  chgrp -R root $pkgdir
}
