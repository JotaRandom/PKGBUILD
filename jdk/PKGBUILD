# Maintainer: Det
# Based on jre: https://aur.archlinux.org/packages.php?ID=51908

pkgname=jdk
_major=8
_minor=5
_build=b13
pkgver=${_major}u${_minor}
pkgrel=1
pkgdesc="Java Development Kit"
arch=('i686' 'x86_64')
url=http://www.oracle.com/technetwork/java/javase/downloads/index.html
license=('custom')
depends=('desktop-file-utils' 'hicolor-icon-theme' 'libx11' 'libxrender' 'libxslt' 'libxtst' 'shared-mime-info' 'xdg-utils')
optdepends=('alsa-lib: sound'
            'ttf-dejavu: fonts')
provides=("java-environment=${_major}" "java-runtime=${_major}" "java-runtime-headless=${_major}" "java-web-start=${_major}")
conflicts=("${provides[@]}" "${provides[@]/${_major}/7}")
backup=('etc/derby.conf'
        'etc/profile.d/jdk.csh'
        'etc/profile.d/jdk.sh')
install=${pkgname}.install
# Workaround for the AUR Web interface Source parser
_arch=x64
_arch2=amd64
if [ "${CARCH}" = 'i686' ]; then
  _arch=i586
  _arch2=i386
fi
source=("http://download.oracle.com/otn-pub/java/jdk/${pkgver}-${_build}/${pkgname}-${pkgver}-linux-${_arch}.tar.gz"
        'derby.conf'
        'derby.service'
        'java-mission-control.desktop'
        'java-monitoring-and-management-console.desktop'
        'java-policy-settings.desktop'
        'java-visualvm.desktop'
        'javaws-launcher'
        'jdk.csh'
        'jdk.sh')
md5sums=(`curl -sL ${url/i*}/javase8-binaries-checksum-2133161.html | sed -nr "s|.*>${source##*/}</td> *<td>(<*[^<]*).*|\1|p"`
         '4bdff6982c66d24a879c424aaac3d04d'  # derby.conf
         '77f3b7ddf55c112f97a665a825bf764f'  # derby.service
         '536ea84bba5eb6bfe8f601b41af9769c'  # java-mission-control.desktop
         'da10de5e6507c392fc9871076ef53ec6'  # java-monitoring-and-management-console.desktop
         'f4e25ef1ccef8d36ff2433c3987a64fe'  # java-policy-settings.desktop
         '35fd89c5c170021d2183593335703703'  # java-visualvm.desktop
         '45c15a6b4767288f2f745598455ea2bf'  # javaws-launcher
         'e81bb7853b071df6adca0b90f0c2ac2d'  # jdk.csh
         'a06d46b277e2926d73d2ee11c9950973') # jdk.sh
## Alternative mirrors, if your local one is throttled:
#source[0]="http://ftp.wsisiz.edu.pl/pub/pc/pozyteczne%20oprogramowanie/java/${pkgname}-${pkgver}-linux-${_arch}.tar.gz"
#source[0]="http://uni-smr.ac.ru/archive/dev/java/SDKs/sun/j2se/${_major}/jdk-${pkgver}-linux-${_arch}.tar.gz"

DLAGENTS=('http::/usr/bin/curl -LC - -b "oraclelicense=a" -O')

package() {
  msg2 "Creating required dirs"
  cd ${pkgname}1.${_major}.0_0${_minor}
  mkdir -p "${pkgdir}"/{opt/java/,usr/{lib/{mozilla/plugins,systemd/system},share/licenses/jdk},etc/{.java/.systemPrefs,profile.d}}

  msg2 "Removing redundancies"
  rm -r db/bin/*.bat jre/{plugin/,COPYRIGHT,LICENSE,*.txt} man/ja # lib/{desktop,visualvm/platform/docs}

  msg2 "Moving stuff in place"
  mv jre/lib/desktop/* man "${pkgdir}"/usr/share/
  mv COPYRIGHT LICENSE *.txt "${pkgdir}"/usr/share/licenses/jdk/
  mv * "${pkgdir}"/opt/java/

  msg2 "Symlinking plugin"
  ln -s /opt/java/jre/lib/${_arch2}/libnpjp2.so "${pkgdir}"/usr/lib/mozilla/plugins/

  msg2 "Installing additions"
  cd "${srcdir}"
  install -m755 jdk.{,c}sh "${pkgdir}"/etc/profile.d/
  install -m644 derby.conf "${pkgdir}"/etc/
  install -m644 derby.service "${pkgdir}"/usr/lib/systemd/system/
  install -m755 javaws-launcher "${pkgdir}"/opt/java/jre/bin/
  install -m644 *.desktop "${pkgdir}"/usr/share/applications/

  msg2 "Tweaking javaws .desktop"
  sed -e 's/Exec=javaws/&-launcher %f/' \
      -e '/NoDisplay=true/d' \
      -i "${pkgdir}"/usr/share/applications/sun-javaws.desktop
}