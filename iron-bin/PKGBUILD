# Maintainer: Sebastien Leduc <sebastien@sleduc.fr>
# Contributor: Elkin Cruz <linuxero789@gmail.com>
# Contributor: Det <nimetonmaili at gmail a-dot com>
# Contributor: larsrh <lars.hupel@gmx.de>
# Contributor: Falcata <zauberexonar@gmail.com>

pkgname=iron-bin
pkgver=28.0.1550.0
pkgrel=1
pkgdesc="A browser based on Chromium without Google's 'tracking features'"
arch=('i686' 'x86_64')
url="http://www.srware.net/en/software_srware_iron.php"
license=('BSD')
depends=('gtk2' 'dbus-glib' 'nss' 'alsa-lib' 'xdg-utils' 'bzip2' 'libevent' 'libxss' 'libgcrypt' 'ttf-font' 'desktop-file-utils' 'hicolor-icon-theme' 'gconf')
optdepends=('kdebase-kdialog: needed for file dialogs in KDE')
install=$pkgname.install
[ "$CARCH" == 'x86_64' ] && _64=-64
source=("http://www.srware.net/downloads/iron-linux$_64.tar.gz"
        iron.desktop
        iron.png
        iron.sh
        iron.default
        LICENSE)
md5sums=('d0821fed53e2098a6dae22d17346e91c'
         '2dc0bd42de6483b63c8ebc71dfcf5a9d'
         '60df76c38381e46577505b0c61cea017'
         '04e84fbe164291915591f1f2e1aa9727'
         '36f606a7e5ae3db2e28daa7c45c18c78'
         '30032c6c531632af6547fa360395acd3')
[ "$_64" == "-64" ] && md5sums[0]='cc6d0f41ea2263daff06953288b83565'

package() {
  # This file is a "ELF 64-bit LSB shared object" that was present in the
  # 32-bits archive in the version 25.0.1400.0 that seems to be a temporary
  # file due to the compilation and should not have been included.
  # It was reported upstream so hopefully this line will not be required
  # in the near future
  rm -f "$srcdir/iron-linux/.goutputstream-JEYXTW"

  install -Dm755 iron.sh "$pkgdir/usr/bin/iron"
  install -d "$pkgdir/opt"
  mv iron-linux$_64 "$pkgdir/opt/iron"

  # Remove the already provided logo
  rm -f "$pkgdir"/opt/iron/product_logo_48.png

  #udev workaround
  ln -s /usr/lib/libudev.so "$pkgdir/opt/iron/libudev.so.0"

  install -Dm644 iron.default "$pkgdir/etc/iron/default"
  install -Dm644 iron.png "$pkgdir/usr/share/pixmaps/iron.png"
  install -Dm644 iron.desktop "$pkgdir/usr/share/applications/iron.desktop"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
