# Maintainer: ava1ar <mail(at)ava1ar(dot)info>

pkgbase=chromium-plugins
pkgname=(chromium-pepper-flash)
pkgdesc="Google Chrome's plugins for Chromium (pepper-flash stable version)"
pkgver=37.0.2062.120
pkgrel=1
epoch=1
_verbld=${pkgver}-1
_channel='stable'
arch=('i686' 'x86_64')
url="http://www.google.com/chrome"
license=('custom:chrome')
source=(license.html::http://www.google.com/chrome/intl/en/eula_text.html)
sha1sums=('SKIP')
if [ "$CARCH" == x86_64 ]; then
	_arch=x86_64
	sha1sums+=('74fdf78ca65f117987a43e71f896a8eb7605a6d2')
elif [ "$CARCH" == i686 ]; then
	_arch=i386
	sha1sums+=('e94917e2e47aa05c0c6764ff2e6a855324e64844')
fi
source+=(https://dl.google.com/linux/chrome/rpm/stable/${_arch}/google-chrome-${_channel}-${_verbld}.${_arch}.rpm)

package_chromium-pepper-flash() {
	pkgdesc="Google Chrome's Pepper Flash plugin for Chromium (stable version)"
	pkgver=15.0.0.152
	conflicts=('chromium-pepper-flash-dev')
	install=chromium-pepper-flash.install

	install -d "${pkgdir}/usr/lib/PepperFlash"
	install -m644 opt/google/chrome/PepperFlash/* "${pkgdir}/usr/lib/PepperFlash"
	sed -i "s/flashver=.*/flashver=${pkgver}/" "${startdir}/chromium-pepper-flash.install"
	# License
	install -Dm644 "${srcdir}/license.html" "${pkgdir}/usr/share/licenses/${pkgname}/license.html"
}
