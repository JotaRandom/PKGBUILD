# Maintainer: Nicolas Avrutin  <nicolasavru@gmail.com>
# Contributor: Pablo Lezaeta <prflr 88 (arro'a) gmail (dot) com>

pkgname=pa-applet-git
pkgver=2013.09.13.git16
pkgrel=1
epoch=1
pkgdesc="PulseAudio system tray applet with volume bar"
arch=(i686 x86_64)
url="https://github.com/fernandotcl/pa-applet"
license=('BSD')
depends=('gtk3' 'libnotify' 'libpulse')
makedepends=('git')
options=('!libtool')
#source=("pa-applet::git+git://github.com/fernandotcl/pa-applet.git")
source=("pa-applet::git+https://github.com/nicolasavru/pa-applet.git")
md5sums=("SKIP")

pkgver(){
        cd "pa-applet"
        echo "2013.09.13.git$(git rev-list --count HEAD | sed 's/-/./g' )"
}

build() {
	cd "$srcdir/pa-applet"

	./autogen.sh
	./configure --prefix=/usr --sbindir=/usr/bin --libdir=/usr/lib \
	--libexecdir=/usr/lib/pa-applet
	make
}

package() {
	cd "$srcdir/pa-applet"
	make DESTDIR="$pkgdir/" sbindir=/usr/bin install
	install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
