# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: flan_suse

# This wallpaper collection contains the following:
#        * Albatross wallpaper
#        * Bluebird wallpaper
#        * Greybird wallpaper

# This wallpaper collection is complementary to the
# Shimmer Project Xfce themes

pkgname=shimmer-wallpapers
_pkgname=Wallpapers
_pkgnamelong="Shimmer Wallpapers"
pkgver=1.0
pkgrel=2
pkgdesc="Complementary wallpaper collection for the Shimmer Project Xfce themes"
arch=(any)
url=http://shimmerproject.org
license=(CCPL:by-sa-3.0)
groups=(xfce-themes-shimmer-collection)
makedepends=(git)
optdepends=('elementary-xfce-icons: matching icon set'
    'xfce-theme-albatross: matching Shimmer Project Xfce theme'
    'xfce-theme-bluebird: matching Shimmer Project Xfce theme'
    'xfce-theme-greybird: matching Shimmer Project Xfce theme')
source=(git://github.com/shimmerproject/$_pkgname.git#commit=ba134cbfa7130e88822f07b18c454a8791d504b8)
sha256sums=('SKIP')
sha512sums=('SKIP')

package() {
    local _picarray=(Albatross/albatross-2009-10.png
        Bluebird/bluebird-2010-08-1920x1440-notext.png
        Greybird/greybird-wall-1920x1200.png)

    install -d "$pkgdir"/usr/share/xfce4/backdrops/
    for i in ${_picarray[@]}; do
        _name=$(basename $(dirname $i))
        install -Dm644 $_pkgname/$i "$pkgdir"/usr/share/backgrounds/$_name.png
        ln -s /usr/share/backgrounds/$_name.png "$pkgdir"/usr/share/xfce4/backdrops/
    done
}
