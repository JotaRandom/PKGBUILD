# Maintainer: Shanto <shanto@hotmail.com>
# Contributor: JIN Xiao-Yong <jinxiaoyong@gmail.com>
# Contributor: Andre Fettouhi <A.Fettouhi@gmail.com>

pkgname=freetype2-infinality
pkgver=2.4.12
pkgrel=2
_pkgdate=20130514
_pkgrel=01
pkgdesc="TrueType font rendering library with infinality patch"
arch=(i686 x86_64)
license=('GPL')
url="http://www.infinality.net/blog/infinality-freetype-patches/"
depends=('zlib' 'bzip2' 'sh')
optdepends=(
	'fontconfig-infinality: Infinality package for fontconfig (required)'
)
conflicts=('freetype2')
provides=("freetype2=$pkgver")
options=('!libtool')
install='install.sh'
backup=('etc/profile.d/infinality-settings.sh')
source=(
	"http://downloads.sourceforge.net/sourceforge/freetype/freetype-${pkgver}.tar.bz2"
	"http://www.infinality.net/fedora/linux/zips/freetype-infinality-${pkgver}-${_pkgdate}_${_pkgrel}-x86_64.tar.bz2"
	"freetype-2.2.1-enable-valid.patch::https://projects.archlinux.org/svntogit/packages.git/plain/trunk/freetype-2.2.1-enable-valid.patch?h=packages/freetype2"
)
build() {
	rm -rf "${srcdir}/freetype-${pkgver}-build"
	cp -a "${srcdir}/freetype-${pkgver}" "${srcdir}/freetype-${pkgver}-build"
	cd "${srcdir}/freetype-${pkgver}-build"
	cat "$srcdir/"{freetype-2.2.1-enable-valid.patch,freetype-entire-infinality-patchset-20130514-01.patch} | patch -Np1
	./configure --prefix=/usr
	make
}

package() {
	cd "${srcdir}/freetype-${pkgver}-build"
	make DESTDIR="${pkgdir}" install
	install -D -T "${srcdir}/infinality-settings.sh" "${pkgdir}/etc/profile.d/infinality-settings.sh"
}

md5sums=('3463102764315eb86c0d3c2e1f3ffb7d'
         '4f5ff3fd3e3e56310953e25eade4a2d3'
         '214119610444c9b02766ccee5e220680')
