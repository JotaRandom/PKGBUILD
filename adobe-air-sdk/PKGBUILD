# Maintainer/Contributor: Prurigro

pkgname=adobe-air-sdk
pkgver=2.6
pkgrel=5
pkgdesc="Adobe Air Software Development Kit and Runtime Environment"
arch=('i686' 'x86_64')
url="http://get.adobe.com/air/"
license=('custom')
if [[ $CARCH == x86_64 ]]; then
        depends=('lib32-gcc-libs' 'lib32-libxml2' 'lib32-nss' 'lib32-gtk2' 'lib32-libxslt')
fi
if [[ $CARCH == i686 ]]; then
        depends=('gcc-libs' 'libxml2' 'nss' 'gtk2' 'libxslt')
fi
install=${pkgname}.install

source=('http://airdownload.adobe.com/air/lin/download/latest/AdobeAIRSDK.tbz2')
md5sums=('5466178bf7764f043d2225e6d018319c')

package(){
    install -d "${pkgdir}"/opt/adobe-air-sdk
    cd "${srcdir}"
    find . ! -name AdobeAIRSDK.tbz2 ! -name AIR\ SDK\ license.pdf | sed -e 's/\.\///g' | while read file; do
        if [ -d "$file" ]; then
            install -d "${pkgdir}"/opt/adobe-air-sdk/"$file"
        elif [ -h "$file" ]; then
            if [ $(file "$file" | grep 'broken' -c) = "0" ]; then
                ln -s $(echo $(file "$file" | grep -o -e \`[^\']*) | sed -e "s/\`//g") "${pkgdir}"/opt/adobe-air-sdk/"$file"
            fi
        else
            install -D "$file" "${pkgdir}"/opt/adobe-air-sdk/"$file"
        fi
    done
    install -d "${pkgdir}"/usr/share/licenses/adobe-air-sdk
    install -D -m644 "${srcdir}"/AIR\ SDK\ license.pdf "${pkgdir}"/usr/share/licenses/adobe-air-sdk/sdk-license.pdf
}
