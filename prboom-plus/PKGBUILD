# Contributor: Ronan Rabouin <darkbaboon*gmail>
# Contributor: Atyth <atyth@at@hylia.de>

pkgname=prboom-plus
pkgver=2.5.1.3
pkgrel=1
pkgdesc="An enhanced version of the PrBoom Doom engine port"
arch=('i686' 'x86_64')
url="http://prboom-plus.sourceforge.net/"
license=('GPL2')
depends=('libgl' 'mesa' 'sdl_image' 'sdl_mixer' 'sdl_net')
optdepends=('freedoom: Free game data' 'fluidsynth: MIDI music output' 'timidity: MIDI music output')
makedepends=('make' 'autoconf' 'automake' 'deutex-devel' 'imagemagick' 'unzip')
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver-src.zip \
        http://download.savannah.gnu.org/releases/freedoom/freedoom-iwad/ultimate/freedoom-ultimate-0.8-beta1.zip \
        $pkgname.desktop \
        $pkgname-game-server.desktop)
md5sums=('c471ca3d731c981d89f05f65c6aa0fc7'
         '057eb33650f3ccaefebe05be11cf1bd2'
         '67a2ae75edf575971928d5104aa6da1b'
         '3137a72a4ed216f862178b434b8e509f')

build() {
  cd "${srcdir}"/$pkgname-$pkgver-src

  tr -d '\r' < bootstrap > bootstrap.fixed
  mv -f bootstrap.fixed bootstrap
  ln -sf "${srcdir}"/freedoom-ultimate-0.8-beta1/doom.wad data/
  sed 's|/games|/bin|g' -i src/Makefile.am
  sed 's|CFLAGS_OPT=.*|:|g' -i configure.ac

  sh bootstrap
  ./configure --prefix=/usr --with-waddir=/usr/share/freedoom
  make || return 1

  for i in stf{god,ouch}0; do
    deutex -doom data -bmp -get $i
    convert $i.bmp $i.png
  done
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver-src
  make DESTDIR="${pkgdir}" install

  install -D -m644 stfgod0.png "${pkgdir}"/usr/share/pixmaps/prboom-plus-game-server.png
  install -m644 stfouch0.png "${pkgdir}"/usr/share/pixmaps/prboom-plus.png

  install -D -m644 "${srcdir}"/$pkgname.desktop "${pkgdir}"/usr/share/applications/$pkgname.desktop
  install -m644 "${srcdir}"/$pkgname-game-server.desktop "${pkgdir}"/usr/share/applications/
}
