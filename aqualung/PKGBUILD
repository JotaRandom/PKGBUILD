# Maintainer: Rob McCathie <korrode AT gmail>
# Contributor: leepesjee <lpeschier at xs4all dot nl>
# Contributor: fancris3 <fancris3 at aol dot com>

pkgname=aqualung
_ver=1.0-rc1
pkgver=1.0rc1
pkgrel=2
pkgdesc="Advanced music player featuring gapless playback"
arch=('i686' 'x86_64')
url="http://aqualung.factorial.hu/"
license=('GPL')
depends=('desktop-file-utils' 'gtk2' 'libxml2' 'alsa-lib' 'libpulse' 'liblrdf' 'libcdio-paranoia' 'libcddb' 'libsamplerate' 'lua51'
         'libsndfile' 'flac' 'libvorbis' 'liboggz' 'speex' 'libmad' 'lame' 'libmodplug' 'libmpcdec' 'mac' 'wavpack' 'ffmpeg')
makedepends=('gettext')
optdepends=('pulseaudio: Support for PulseAudio'
            'oss: Support for the Open Sound System'
            'jack: Support for JACK (requires rebuild)'
            'libifp: Support iRiver iFP driver (requires rebuild)')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${_ver}.tar.gz"
        "${pkgname}.desktop")
sha1sums=('3b67eeb89f714e760d6a37c03c2f2155518ce483'
          '498b4221445d046dcbc17619156425aa813dc635')
install="${pkgname}.install"

prepare() {
  cd "${srcdir}/${pkgname}-${_ver}"
  ## Remove annoying "http://" preset at 'Add URL'
  sed -i 's|(url_entry), "http://"|(url_entry), ""|' ./src/playlist.c
}

build() {
  cd "${srcdir}/${pkgname}-${_ver}"
  ./autogen.sh
  ./configure --prefix=/usr
  ## Alternate configure line to force ALSA-only output
  #./configure --prefix=/usr --with-jack=no --with-pulse=no --with-oss=no
  make
}

package() {
  cd "${srcdir}/${pkgname}-${_ver}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 "src/img/icon_16.png" "${pkgdir}/usr/share/icons/hicolor/16x16/apps/${pkgname}.png"
  install -Dm644 "src/img/icon_24.png" "${pkgdir}/usr/share/icons/hicolor/24x24/apps/${pkgname}.png"
  install -Dm644 "src/img/icon_32.png" "${pkgdir}/usr/share/icons/hicolor/32x32/apps/${pkgname}.png"
  install -Dm644 "src/img/icon_48.png" "${pkgdir}/usr/share/icons/hicolor/48x48/apps/${pkgname}.png"
  install -Dm644 "src/img/icon_64.png" "${pkgdir}/usr/share/icons/hicolor/64x64/apps/${pkgname}.png"
  install -Dm644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
}
