# Contributor: Jeremy Cowgar <cowgar.com: jeremy>
# Maintainer:  Jesse Jaara   <gmail.com:  jesse.jaara>

pkgname=euphoria
pkgver=4.0.5
pkgrel=1
pkgdesc="Fast interpreted or compiled general purpose programming language"
url="http://openeuphoria.org"
license=('custom:"OpenEuphoria License"')
source=(http://downloads.sourceforge.net/rapideuphoria/Euphoria/${pkgver}/${pkgname}-LINUX-${pkgver}-src.tar.gz)
arch=('i686' 'x86_64')

if [ "$CARCH" = "x86_64" ]; then
  depends=('lib32-glibc')
  makedepends=('gcc-multilib')
fi

build() {
  ### We have a dirty tarball at the moment :(
  cd "${srcdir}/${pkgname}-LINUX-${pkgver}/source"

  ./configure --prefix=/usr

  ## Thearded building is broken in this release. You can also do
  ## makepkg && makepkg -e, if you remove the -j1.
  make -j1
  make tools
}

package() {
  cd "${srcdir}/${pkgname}-LINUX-${pkgver}/source"

  ## The installer cretes the bin dir too late :'( ##
  mkdir -p "${pkgdir}/usr/bin"
  # Base and Tool install
  make DESTDIR="${pkgdir}" install install-tools

  # Install our license file per ArchLinux spec
  mkdir -p "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m 644 "${srcdir}/${pkgname}-LINUX-${pkgver}/License.txt" "${pkgdir}/usr/share/licenses/${pkgname}/"

  # Make an initial eu.cfg
  cat > eu.cfg <<EOF
[all]
-i /usr/share/${pkgname}/include
EOF
  mkdir -p "${pkgdir}/etc/${pkgname}"
  install -m644 eu.cfg "${pkgdir}/etc/${pkgname}"
}

md5sums=('e1b16647698487192f54d4d949e6ca62')
