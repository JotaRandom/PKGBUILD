# Maintainer: Diego Principe <cdprincipe@at@gmail@dot@com>

_pkgname=xfce4-whiskermenu-plugin
pkgname=xfce4-whiskermenu-plugin-git
pkgver=1.3.0.51.g9f63983
pkgrel=1
pkgdesc="Alternate Xfce menu"
arch=('i686' 'x86_64')
url="http://gottcode.org/${_pkgname}/"
license=('GPL2')
depends=('xfce4-panel')
makedepends=('cmake' 'git')
provides=('xfce4-whiskermenu-plugin')
conflicts=('xfce4-whiskermenu-plugin')
install=${pkgname}.install
source=("$_pkgname::git+http://github.com/gottcode/xfce4-whiskermenu-plugin")
md5sums=('SKIP')

pkgver() {
  cd ${srcdir}/${_pkgname}
  git describe --always | sed 's|-|.|g' | grep -o '[0-9]..*[0-9]'
}

build() {
  cd ${srcdir}/${_pkgname}
  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd ${srcdir}/${_pkgname}
  make install/strip DESTDIR=${pkgdir}
}
