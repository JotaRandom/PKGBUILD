# Wuala: Installer: Arch
# Maintainer: Sven Schober <sschober (at) sssm (dot) de>
# Contributor: Xavion <Xavion (dot) 0 (at) Gmail (dot) com>

pkgname=wuala
pkgver=1
pkgrel=20130822
pkgdesc="A new way of storing, sharing and publishing files on the Internet.  It's free, simple and secure."
arch=("any")
url="http://www.wuala.com/"
license=("custom")
depends=("java-runtime" "rpcbind" "nfs-utils")
options=(!emptydirs)
source=("http://cdn.wuala.com/repo/other/wuala.tar.gz"
	"0001-fix-loader3-location.patch"
	"${pkgname}.png"
	"${pkgname}.desktop"
	"LICENSE")

md5sums=('30f137a989d0b2b4d4927d0310a9153f'
         '37f5021c623c4b2fcad6d274a05de0dd'
         '5c0d0ec01eaa7075fc7e3197d2a0a2ee'
         '6073b5fdda17ab8b423bde329adfa80f'
         '1085f1b2c391b8c72cb4b53b15191336')
sha1sums=('513526176723436979da3ccb02f8813d1de91c0b'
          '76b0325499a3b378faafeb0598ab895eee9924a1'
          'cae514b2e405ad0dfbad7cdbe04b7f82bd6294a9'
          'a3d25486b347e03143b1d86ff2a7d83f03ed4bad'
          '77427bc6023ad469b2158b1a22d4fea997244681')

package() {
	cd ${srcdir}/${pkgname}

	patch -Np1 -i ${srcdir}/0001-fix-loader3-location.patch || return 1

	JAVA_DIR=${pkgdir}/usr/share/java/${pkgname}
	DOC_DIR=${pkgdir}/usr/share/doc/${pkgname}
	BIN_DIR=${pkgdir}/usr/bin

	# Directories
	install -d ${JAVA_DIR} ${DOC_DIR} ${BIN_DIR}

	# binary
	install -D wuala{,cmd} ${pkgdir}/usr/bin/

	# jar
	install -D loader3.jar ${JAVA_DIR}/

	# readme and copyright
	install -D readme.txt copyright ${DOC_DIR}


	# Desktop
	install -D ${srcdir}/${pkgname}.desktop ${pkgdir}/usr/share/applications/${pkgname}.desktop
	install -D ${srcdir}/${pkgname}.png ${pkgdir}/usr/share/icons/hicolor/64x64/apps/${pkgname}.png

	# License
	install -D ${srcdir}/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE

}
