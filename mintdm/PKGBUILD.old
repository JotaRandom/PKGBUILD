# Maintainer: twa022 <twa022 at gmail dot com>
# Contributor: Pablo Lezaeta <prflr88 (arroba) gmail (dot) com>
# Contributors:  Philip MÃ¼ller <philm[at]manjaro[dog]org>
# Contributors:  Roland Singer <roland[at]manjaro[dog]org>

pkgname=mintdm
_name=mdm
pkgver=1.3.1
pkgrel=1
pkgdesc="Linux Mint display manager, a fork of GDM 2.20.x"
arch=('i686' 'x86_64')
url="http://www.linuxmint.com"
license=('GPL')
depends=('pam>=1.0.2' 'libdmx' 'gtk2>=2.6' 'libgnomecanvas>=1.109.0' 'librsvg>=1.1.1' 
         'libxml2>=2.4.12' 'libart-lgpl>=2-0' 'dbus-glib>=0.80' 'libwebkit')
makedepends=('gnome-common' 'intltool>=0.35.0' 'perl>=5.8.1' 'gnome-doc-utils>=0.3.2' 'xorg-server' 'pango' 'zenity')
provides=('gdm=2.20.11' 'gdm-old=2.20.11')
conflicts=('gdm')
options=('!libtool')
install='mdm.install'
source=("$_name-$pkgver.zip::https://github.com/linuxmint/$_name/archive/$pkgver.zip"
	'mdm.pam'
	'mdm.service'
	'mdm-autologin.pam'
	'defaults.conf'
	'org.cinnamon.pkexec.mdmsetup.policy'
	'mdm-plymouth.service')
noextract=()

build() {
	cd ${srcdir}/${_name}-${pkgver}
	chmod +x ./autogen.sh
	export AUTOMAKE=automake
	./autogen.sh --enable-ipv6=yes --with-prefetch \
	--prefix=/usr --with-console-kit=no --sysconfdir=/etc \
	--with-systemd --libexecdir=/usr/lib/mdm --localstatedir=/var/lib \
	--disable-static --with-xevie=yes --disable-scrollkeeper \
	--enable-secureremote=yes --enable-ipv6=yes \
	--enable-compile-warnings=no --sbindir=/usr/bin \
	
	LDFLAGS="-lXau -lm"
	
	sed -i -e 's|${prefix}|/usr|' config.h
	make DESTDIR=$pkgdir
}

package() {
	cd ${srcdir}/${_name}-${pkgver}
	make DESTDIR=$pkgdir install

msg2 'Adding PAM rules'
	#PAM, we use our own, not LinuxMint stuff, problem?...
	install -m755 -d $pkgdir/etc/pam.d
	install -m644 $srcdir/mdm-autologin.pam $pkgdir/etc/pam.d/mdm-autologin
	install -m644 $srcdir/mdm.pam $pkgdir/etc/pam.d/mdm

msg2 'Adding SystemD services'
	#systemd Init script, but ... SysV Inint is soo old
	install -m755 -d $pkgdir/usr/lib/systemd/system
	install -m644 $srcdir/mdm.service $pkgdir/usr/lib/systemd/system/mdm.service

msg2 'Adding config files'
	#configuration
	install -m444 "${srcdir}/defaults.conf" "${pkgdir}/usr/share/mdm/" 
	rm -f ${pkgdir}/usr/share/xsessions/gnome.desktop

msg2 'Adding PolicyKit execution scheme'
	#PolicyKit execution scheme (need some tests)
	install -m755 -d $pkgdir/usr/share/polkit-1/actions/
	install -m644 $srcdir/org.cinnamon.pkexec.mdmsetup.policy $pkgdir/usr/share/polkit-1/actions/

	#Why on the hay this directory is created empty in etc, remove it 
	rmdir $pkgdir/etc/dm

msg2 'Adding faces'
	#Fix mdm files conflict with gnome-control-center (usr/share/pixmaps/faces/*)
	install -m755 -d $pkgdir/usr/share/pixmaps/faces/

	make DESTDIR=$pkgdir install -C gui/faces

msg2 'Adding aplications'
	#Fix gdmsetup.desktop
	sed -i -e 's|^Exec=gksu|Exec=pkexec|' $pkgdir/usr/share/mdm/applications/mdmsetup.desktop
	sed -i -e 's|^NoDisplay=true|NoDisplay=false |' $pkgdir/usr/share/mdm/applications/mdmphotosetup.desktop

	#Fix erroneous path for certain aplications
	install -m755 -d $pkgdir/usr/share/applications
	mv -f $pkgdir/usr/share/mdm/applications/*.* $pkgdir/usr/share/applications
	chmod 755 $pkgdir/usr/share/applications/*.*
	rmdir $pkgdir/usr/share/mdm/applications

}

sha256sums=('6de10a12f8df88e1e9fba905df636eaf9c18fb020e3588f1591ce572d7234ce0'
            '5ac72cc98cd1cf24bddb29e5d398133641a2768e064c79b104e317d2796cc071'
            'd3dd582eb25bb31f012167069c869d3ce89c1dbd9b5aa7396350d8cf609994a5'
            '479e8fd4d5ef353fec5af776ad05e4f414a92d6f374f9b8f5deef77b9e301bc7'
            'ce9448356c54d8de5b8af5cdef2b5c30b4e6ae970126ce0c4ef295be557bbd2a'
            'c2cbe920d801a978510b8c15066742bd9ff6916b6d4944efa0269a3022ae19af'
            '7579ed402ee0db60e0c40ddbb7b2c80be0aec4315e45aa525908bef8aa181bd6')
