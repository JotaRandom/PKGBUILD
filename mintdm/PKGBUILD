# Maintainer: Philip MÃ¼ller <philm[at]manjaro[dog]org>
# Maintainer: Roland Singer <roland[at]manjaro[dog]org>
# Contributors: twa022 <twa022 at gmail dot com>
# Contributors: Pablo Lezaeta <prflr88 (arroba) gmail (dot) com>

pkgname=mdm
pkgver=1.3.1
pkgrel=2
pkgdesc="Linux Mint display manager, a fork of GDM 2.20.x"
arch=('i686' 'x86_64')
url="http://www.linuxmint.com"
license=('GPL')
groups=()
depends=('pam>=1.0.2' 'libdmx' 'gtk2>=2.6' 'libgnomecanvas>=1.109.0' 'librsvg>=1.1.1' 'libxml2>=2.4.12' 'libart-lgpl>=2-0'
'dbus-glib>=0.80' 'libwebkit' 'xdg-su')
makedepends=('gnome-common' 'intltool>=0.35.0' 'perl>=5.8.1' 'gnome-doc-utils>=0.3.2' 'xorg-server' 'pango' 'zenity' 'popt')
optdepends=('webkitgtk: mdm-theme-emulator' 'xorg-server-xnest: mdmthemetester')
provides=('gdm' 'gdm-old' "mintdm=$pkgver")
conflicts=('gdm' 'mintdm')
replaces=()
backup=('etc/mdm/custom.conf' 'etc/pam.d/mdm' 'etc/pam.d/mdm-autologin')
options=('!libtool')
install='mdm.install'
changelog=
source=("$pkgname-$pkgver.zip::https://github.com/linuxmint/$pkgname/archive/$pkgver.zip"
'mdm.pam'
'mdm.service'
'mdm-autologin.pam'
'defaults.conf'
'mdm-plymouth.service')
noextract=()

build() {
	cd ${srcdir}/${pkgname}-${pkgver}
	chmod +x ./autogen.sh
	export AUTOMAKE=automake
	./autogen.sh --enable-ipv6=yes --with-prefetch \
	--prefix=/usr --with-console-kit=no --sysconfdir=/etc \
	--with-systemd --libexecdir=/usr/lib/mdm --localstatedir=/var/lib \
	--disable-static --with-xevie=yes --disable-scrollkeeper \
	--enable-secureremote=yes --enable-ipv6=yes \
	--enable-compile-warnings=no --sbindir=/usr/bin \
	LDFLAGS="-lXau -lm"
	sed -i -e 's|${prefix}|/usr|' config.h
	make DESTDIR=$pkgdir
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}

	make DESTDIR=$pkgdir install

msg2 'Adding PAM rules'
	#PAM, we use our own, not LinuxMint stuff, problem?...
	install -m755 -d $pkgdir/etc/pam.d
	install -m644 $srcdir/mdm-autologin.pam $pkgdir/etc/pam.d/mdm-autologin
	install -m644 $srcdir/mdm.pam $pkgdir/etc/pam.d/mdm

msg2 'Adding SystemD services'
	#systemd Init script, but ... SysV Inint is soo old
	install -m755 -d $pkgdir/usr/lib/systemd/system
	install -m644 $srcdir/mdm.service $pkgdir/usr/lib/systemd/system/mdm.service

msg2 'Adding config files'
	#configuration
	install -m444 "${srcdir}/defaults.conf" "${pkgdir}/usr/share/mdm/"
	rm -f ${pkgdir}/usr/share/xsessions/gnome.desktop || true

msg2 'Adding faces'
	#Fix mdm files conflict with gnome-control-center (usr/share/pixmaps/faces/*)
	install -m755 -d $pkgdir/usr/share/pixmaps/faces/

	make DESTDIR=$pkgdir install -C gui/faces

msg2 'Adding aplications'
	#Fix gdmsetup.desktop
	sed -i -e 's|^Exec=gksu|Exec=xdg-su |' $pkgdir/usr/share/mdm/applications/mdmsetup.desktop
	sed -i -e 's|^Categories=|Categories=GNOME;GTK;System;Settings;X-GNOME-Settings-Panel;X-GNOME-SystemSettings;X-XFCE-SettingsDialog;X-XFCE-SystemSettings;|' $pkgdir/usr/share/mdm/applications/mdmsetup.desktop	
	sed -i -e 's|^NoDisplay=true|NoDisplay=false |' $pkgdir/usr/share/mdm/applications/mdmphotosetup.desktop
	sed -i -e 's|^Categories=|Categories=GNOME;GTK;Settings;DesktopSettings;X-XFCE-SettingsDialog;X-XFCE-PersonalSettings;|' $pkgdir/usr/share/mdm/applications/mdmphotosetup.desktop

#Fix erroneous path for certain aplications
	install -m755 -d $pkgdir/usr/share/applications
	mv -f $pkgdir/usr/share/mdm/applications/*.* $pkgdir/usr/share/applications
	chmod 755 $pkgdir/usr/share/applications/*.*
	rmdir $pkgdir/usr/share/mdm/applications

}


md5sums=('f1ac7be3d483ad3b18a86a96b32972b4'
         '80052ef367704c71addedcdf4e4a54e9'
         '9a8208efbedcf77c1d5df6dd8a2ca092'
         '51a7cb2140731263ac9cb634fd1ca477'
         'daff2050b02f73f59106e7bfe66e9b3c'
         'fa711f1dc5bd192cd41fc1ee93c369a5')
