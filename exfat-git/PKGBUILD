# Maintainer: rtfreedman  <rob<d0t>til<d0t>freedman< T>googlemail<d0t>com>
#
# The code and the license of this repository is of unkown origin.
# Using it might be a problem in your country - in most others it isn't ;)
#
# If you're unsure, use it only on your own private systems
# and not inside your company or systems facing the public.
#
# This PKGBUILD will be available as long as the repository is public.
#
# For discussions about the git repo, see:
# http://phoronix.com/forums/showthread.php?81642-Native-Linux-Kernel-Module-Is-Out-For-Microsoft-exFAT
#

pkgname=exfat-git
pkgver=37.3782f98
pkgrel=1
url='https://github.com/rxrz/exfat-nofuse'
pkgdesc='Native kernel module for extended fat support'
license=('Samsung Proprietary')
#
arch=('i686' 'x86_64')
depends=('linux>=3.8' 'linux<3.11')
makedepends=('linux-headers' 'git')
options=('!strip')
#
install="${pkgname}.install"
source=("${pkgname}::git+https://github.com/rxrz/exfat-nofuse.git")
md5sums=('SKIP')

pkgver() {
  cd "${pkgname}"
  echo $(git rev-list --count master).$(git rev-parse --short master)
}

#
# Put back gpl symbol for private & testing purpose only
#
prepare() {
  cd "${pkgname}"
  sed 's|MODULE_LICENSE("Samsung Proprietary")|MODULE_LICENSE("GPL")|' \
   -i exfat_api.c exfat_super.c
}

# Find all kernels and extramodule directories
KERNELS=`cat /usr/lib/modules/extramodules*/version`
XTRAMOD=`find /usr/lib/modules -name version | sed 's|\/usr\/lib\/modules\/||; s|\/version||'`

build() {
  cd "${pkgname}"
  for _kernver in $KERNELS; do
    echo Building module for $_kernver
    make KDIR=/usr/lib/modules/${_kernver}/build M="$(pwd)"
    mkdir -p ${_kernver}
    mv exfat_core.ko exfat_fs.ko ${_kernver}
    rm *.o
  done
}

package() {
  cd "${pkgname}"
  # Loop through all detected kernels
  for _kernver in $KERNELS; do
	# Loop through all detected extramodules directories
	for _mod_dir in $XTRAMOD; do
	  # Check which extramodules directory corresponds with the built module
	  if [ $(cat "/usr/lib/modules/${_mod_dir}/version") = ${_kernver} ]; then
	    gzip ${_kernver}/{exfat_core.ko,exfat_fs.ko}
	    install -m755 -d "${pkgdir}/usr/lib/modules/${_mod_dir}/"
	    install -m644 -t "${pkgdir}/usr/lib/modules/${_mod_dir}/" \
			${_kernver}/{exfat_core.ko.gz,exfat_fs.ko.gz}
	  fi
	done
  done
  # no license yet
  #cd "$srcdir/$pkgname"
  #install -Dm755 LICENSE "${pkgdir}"/usr/share/licenses/exfat-git/LICENSE
}
