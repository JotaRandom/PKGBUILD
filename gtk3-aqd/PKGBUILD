# $Id$
# Maintainer: Victor Aur√©lio <victoraur.santos@gmail.com>

pkgname=gtk3-aqd
pkgver=3.8.2
pkgrel=1
pkgdesc="GObject-based multi-platform GUI toolkit (v3)"
arch=('i686' 'x86_64')
url="http://www.gtk.org/"
install=gtk3.install
depends=('atk>=2.7.5' 'glib2>=2.35.3' 'gdk-pixbuf2>=2.27.1' 'cairo' 'gtk-update-icon-cache' 'libcups' 'libxcursor' 'libxinerama' 'libxrandr' 'libxi' 'libxcomposite' 'libxdamage' 'pango' 'shared-mime-info' 'colord' 'at-spi2-atk')
makedepends=('gobject-introspection')
provides=('gtk3')
replaces=('gtk3')
conflicts=('gtk3')
options=('!libtool')
backup=(etc/gtk-3.0/settings.ini)
license=('LGPL')
source=(http://ftp.gnome.org/pub/gnome/sources/gtk+/${pkgver%.*}/gtk+-$pkgver.tar.xz
        settings.ini
        gtk-gtkmenubar.patch)

prepare() {
    cd $srcdir/gtk+-$pkgver

    ## Apply "aqd" patch
    patch -Np0 < $srcdir/gtk-gtkmenubar.patch
}

build(){
cd $srcdir/gtk+-$pkgver
CXX=/bin/false ./configure --prefix=/usr \
        --sysconfdir=/etc --libdir=/usr/lib \
        --localstatedir=/var --libexecdir=/usr/lib/gtk-3.0 \
        --enable-gtk2-dependency --sbindir=/usr/bin \
        --disable-schemas-compile --bindir=/usr/bin \
	--enable-x11-backend --enable-broadway-backend \
	--enable-wayland-backend
    
#https://bugzilla.gnome.org/show_bug.cgi?id=655517
sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

make
}

package() {
    cd "gtk+-$pkgver"
    make DESTDIR="$pkgdir" sbindir=/usr/bin install

    install -Dm644 "$srcdir/settings.ini" "$pkgdir/etc/gtk-3.0/settings.ini"
}
md5sums=('8e878e18fc385f2b813419dc7b40a968'
         '085ece008fa3a0b7a72e9fe3dda3631a'
         '54b6bf2d97ca977de248a38eae8e9619')
