# Maintainer: Alucryd <alucryd at gmail dot com> 
# Contributor: Ner0 <darkelfdarkelf666@yahoo.co.uk>
# Contributor: dcelasun <dcelasun@gmail.com>

pkgname=plank-bzr
pkgver=865
pkgrel=1
pkgdesc="Elegant, simple, clean dock"
arch=('i686' 'x86_64')
url="https://launchpad.net/plank"
license=('GPL3')
groups=('pantheon')
depends=('bamf-bzr' 'hicolor-icon-theme' 'libdbusmenu-gtk3' 'libgee06')
makedepends=('bzr' 'gnome-common' 'intltool')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
options=('!libtool')
install=${pkgname%-*}.install
source=("bzr+lp:${pkgname%-*}")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}"/${pkgname%-*}

  bzr revno
}

prepare() {
  cd "${srcdir}"/${pkgname%-*}

  sed -i 's|^#!.*python$|#!/usr/bin/python2|' $(grep -rl '^#!.*python')
}

build() {
  cd "${srcdir}"/${pkgname%-*}

  ./autogen.sh --prefix=/usr --sysconfdir=/etc --disable-gee-0.8
  make
}

package() {
  cd "${srcdir}"/${pkgname%-*}

  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
