# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>
# Contributor: Ner0


pkgname=chromium-pepper-flash-dev
pkgver=17.0.0.149
pkgrel=1
pkgdesc="Google Chrome's Pepper Flash plugin for Chromium (Dev Channel)"
arch=('i686' 'x86_64')
url="http://www.google.com/chrome"
license=('custom:chrome')
optdepends=('pulseaudio-alsa: For PulseAudio users')
conflicts=('chromium-pepper-flash' 'chromium-pepper-flash-stable')
provides=('chromium-pepper-flash')
options=('!emptydirs' '!strip')
install=chromium-pepper-flash-dev.install
_branch=unstable

[ "${CARCH}" = "i686" ] && _rpm_arch="i386"
[ "${CARCH}" = "x86_64" ] && _rpm_arch="x86_64"

sha1sums=('ec6cdf6105886c2ba83b2a380256452323b07b1e')
source=('LICENSE.txt')
_metadata="$(curl -sL "https://dl.google.com/linux/chrome/rpm/stable/${_rpm_arch}/repodata/other.xml.gz" | gzip -df)"
_rpm_ver="$(echo "${_metadata}" | grep -e "${_branch}" | awk -v FS='ver="' -v RS='" ' '$0=$2'  | head -n 1)"
_rpm_rel="$(echo "${_metadata}" | grep -e "${_branch}" | awk -v FS='rel="' -v RS='"/' '$0=$2' | head -n 1)"
_rpm_sha1sum="$(echo "${_metadata}" | grep -e "${_branch}" | awk -v FS='pkgid="' -v RS='" ' '$0=$2' | head -n 1)"
source+=("http://dl.google.com/linux/chrome/rpm/stable/${_rpm_arch}/google-chrome-${_branch}-${_rpm_ver}-${_rpm_rel}.${_rpm_arch}.rpm")
sha1sums+=("${_rpm_sha1sum}")
noextract=("google-chrome-${_branch}-${_rpm_ver}-${_rpm_rel}.${_rpm_arch}.rpm")

pkgver() {
  bsdtar -xf "google-chrome-${_branch}-${_rpm_ver}-${_rpm_rel}.${_rpm_arch}.rpm" opt/google/chrome*/PepperFlash
  echo "$(sed -n 's/.*"version": "\(.*\)",.*/\1/p' opt/google/chrome*/PepperFlash/manifest.json)"
}

package() {
  install -Dm644 opt/google/chrome*/PepperFlash/manifest.json "${pkgdir}/usr/lib/PepperFlash/manifest.json"
  install -Dm755 opt/google/chrome*/PepperFlash/libpepflashplayer.so "${pkgdir}/usr/lib/PepperFlash/libpepflashplayer.so"

  install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -d "${pkgdir}/etc/chromium"
  echo -n "CHROMIUM_FLAGS=\"\${CHROMIUM_FLAGS} " > "${pkgdir}/etc/chromium/pepper-flash"
  echo -n "--ppapi-flash-path=/usr/lib/PepperFlash/libpepflashplayer.so " >> "${pkgdir}/etc/chromium/pepper-flash"
  echo "--ppapi-flash-version=${pkgver}\"" >> "${pkgdir}/etc/chromium/pepper-flash"
  install -Dm644 "${pkgdir}/etc/chromium/pepper-flash" "${pkgdir}/etc/chromium-dev/pepper-flash"
  install -Dm644 "${pkgdir}/etc/chromium/pepper-flash" "${pkgdir}/etc/chromium-browser/pepper-flash"
  install -Dm644 "${pkgdir}/etc/chromium/pepper-flash" "${pkgdir}/etc/iron/pepper-flash"
}
