# Maintainer: bzt <unmacaque at gmail.com>
# Contributor: Boohbah <boohbah at gmail.com>

pkgname=snes9x-git
pkgver=1.53_230_gdfc6227
pkgrel=1
pkgdesc="A GTK+ port of the Snes9x emulator (git version)"
arch=('i686' 'x86_64')
url="https://github.com/snes9xgit/snes9x"
license=('LGPL')
depends=('zlib' 'libpng' 'sdl' 'alsa-lib' 'libglade' 'libxv' 'gcc-libs' 
         'libxext' 'libsm' 'libgl' 'gtk3')
makedepends=('intltool' 'nasm' 'mesa' 'git' 'autogen')
provides=('snes9x' 'snes9x-gtk')
conflicts=('snes9x' 'snes9x-gtk')
install=snes9x-git.install
source=('Snes9x::git+http://github.com/snes9xgit/snes9x')
md5sums=('SKIP')

pkgver() {
	cd "Snes9x"
	git describe --always | sed 's#-#_#g;s#v##'
}

build() {
  cd "$srcdir/Snes9x/gtk"
  ./autogen.sh
  ./configure --prefix=/usr --with-netplay --with-gtk3 \
	--sbindir=/usr/bin --libexecdir=/usr/lib/snes9x \
	--bindir=/usr/bin --libdir=/usr/lib 
  make
}

package() {
  cd "$srcdir/Snes9x/gtk"
  make DESTDIR=${pkgdir} install
}
