# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=ubuntu-wallpapers
_ubuntu_rel=0ubuntu1
pkgver=14.04.0.1
pkgrel=1
pkgdesc="The default wallpapers for Ubuntu (from 9.10 Karmic Koala to 13.10 Saucy Salamander)"
arch=('any')
url="https://launchpad.net/ubuntu-wallpapers"
license=('GPL')
groups=('unity-extra')
makedepends=('libxslt' 'python2-distutils-extra')
install=${pkgname}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${pkgver}.orig.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${pkgver}-${_ubuntu_rel}.diff.gz")
sha512sums=('c3b5addec0fbe0974a1263bbb8fb7b09a924d8b80a5bfe042e416721cd28cbdb3871e625af665985eb1d82ddbf40daa1a0397a097bf963a658ad74145abeaca8'
            'de49bb8b4b4933ae0de0260612a20a13403f9b4a595635665966653822b328e8600f0a848f40f344b46cc2b8889879482ed71c4ab4e7f4ca70dde4836b3dfc41')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # The setup.py script reads the package version from debian/*
  patch -p1 -i "${srcdir}/${pkgname}_${pkgver}-${_ubuntu_rel}.diff"
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  python2 setup.py install --root="${pkgdir}/" --optimize=1
}

# vim:set ts=2 sw=2 et:
