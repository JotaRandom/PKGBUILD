# Maintainer: Aljosha Papsch <papsch.al@gmail.com>
# Contributor: Benjamin Wild <benwild@gmx.de>
# Contributor: Brian Bidulock <bidulock@openss7.org>

pkgname=network-manager-applet-gtk2
_pkgname=network-manager-applet
pkgver=0.9.8.4
pkgrel=1
pkgdesc="GNOME frontends to NetWorkmanager compiled for gtk2"
arch=(i686 x86_64)
license=(GPL)
url="http://www.gnome.org/projects/NetworkManager/"
depends=("networkmanager>=${pkgver::5}" libsecret gtk2 libnotify
         gnome-icon-theme mobile-broadband-provider-info iso-codes)
makedepends=(intltool gnome-bluetooth gobject-introspection)
optdepends=('gnome-bluetooth: for PAN/DUN support')
options=('!libtool' '!emptydirs')
provides=("$_pkgname=$pkgver")
conflicts=("$_pkgname")
install=network-manager-applet.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$_pkgname/0.9/$_pkgname-$pkgver.tar.xz)
sha256sums=('f68849291047c94487279aab4a6cb90945ad351df02da621a73b413e6c6c198c')

build() {
  cd ${_pkgname}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc --with-gtkver=2 \
      --localstatedir=/var \
      --libexecdir=/usr/lib/networkmanager \
      --disable-static \
      --disable-more-warnings \
      --disable-maintainer-mode \
      --disable-migration \
      --with-modem-manager-1
  make
}

package() {
  cd ${_pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
