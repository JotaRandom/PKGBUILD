# Contributer: Alucryd <alucryd at gmail dot com> 
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Kessia 'even' Pinheiro <kessiapinheiro at gmail.com>
# Contributor: peace4all <markspost at rocketmail dot com>
# Maintainer Pablo Lezaeta <prflr88@gmail.com>

pkgname=gufw
pkgver=13.04.0
pkgrel=3
pkgdesc="An easy, intuitive way to manage your Linux firewall"
arch=('any')
url="http://gufw.org"
license=('GPL3')
depends=('dbus' 'dbus-python' 'gtk3' 'gnome-icon-theme-symbolic' 'hicolor-icon-theme' 'notification-daemon' 'polkit' 'python2-gobject' 'ufw')
makedepends=('python2-distutils-extra')
install=$pkgname.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/gui-ufw_${pkgver}.orig.tar.gz"
	"org.gui-ufw.pkexec.gufw.policy")

package() {
  cd "${srcdir}/gui-ufw-${pkgver}"

  sed -i -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
    -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
    $(find . -name '*.py')
  sed -i "s|python ${LOCATIONS[${i}]}|python2 ${LOCATIONS[${i}]}|" bin/gufw

  python2 setup.py build install --root="${pkgdir}"

  find "${pkgdir}" -type d | xargs chmod 755
  find "${pkgdir}" -type f | xargs chmod 644
  chmod -R 755 "${pkgdir}/usr/bin"
  chmod 755 "${pkgdir}/usr/share/gufw/daemon/gufw_daemon.py"

  #PolicyKit execution scheme (need some tests)
  install -m755 -d $pkgdir/usr/share/polkit-1/actions/
  install -m644 $srcdir/org.gui-ufw.pkexec.gufw.policy $pkgdir/usr/share/polkit-1/actions/

}

md5sums=('59c220d030ec11b5fb0b9affa9e833a2'
         '47e7779a8c6acb69ab11ff7b87599359')
