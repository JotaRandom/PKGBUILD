# Contributor: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Maintainer: Pablo Lezaeta <prflr88@gmail.com>

pkgname=xubuntu-artwork
pkgver=14.04.2
pkgrel=1
pkgdesc="Xubuntu themes and artwork"
arch=('any')
url="https://launchpad.net/xubuntu-artwork"
license=('GPL')
depends=('xfce-theme-albatross' 'xfce-theme-bluebird' 'xfce-theme-greybird' 'shimmer-wallpapers')
optdepends=('plymouth: For plymouth theme to work'
        'lightdm-gtk3-greeter: For LightDM GTK-3 Greeter theme to work or if you want use the GTK-2 version'
        'shimmer-wallpapers: Wallpapers not included in the package because ...'
        'xfce-theme-albatross: Official theming, Git or stable versions are ok'
        'xfce-theme-bluebird: Official theming, Git or stable version are ok'
        'xfce-theme-greybird: Official theming, Git or stable version are ok'
	'elementary-xfce-icons: For matching icon theme, or if you want use the git version')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/xubuntu-artwork_${pkgver}.tar.gz"
        '0001_Plymouth_dir.patch')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -p1 -i "${srcdir}/0001_Plymouth_dir.patch"
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  install -dm755 "${pkgdir}/usr/"

  msg2 'Install plymouth theme.'
  mkdir -p "${pkgdir}/usr/share/"
  cp -av lib/plymouth/ "${pkgdir}/usr/share/"

  msg2 'Install the rest of the files.'
  cp -av usr/share/ "${pkgdir}/usr/"

  msg2 'Remove Elementary-Xfce bundled with this artwork, is outdated.'
  rm -frv "${pkgdir}"/usr/share/icons

  msg2 'Move backdrops to a better place for Xfce 4.12+.'
  mkdir -p "${pkgdir}/usr/share/backgrounds/xfce"
  mv -v "${pkgdir}"/usr/share/xfce4/backdrops/* "${pkgdir}"/usr/share/backgrounds/xfce
  rm -frv "${pkgdir}/usr/share/xfce4"
}

md5sums=('471e6e17b1ba452bfc525dd102f7adc4'
         'c9a1b1997abf7d43dda91bf7991ed42c')
