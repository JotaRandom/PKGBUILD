# Contributor: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Maintainer: Pablo Lezaeta <prflr88@gmail.com>

pkgname=xubuntu-artwork
pkgver=13.10.3
pkgrel=3
pkgdesc="Xubuntu themes and artwork"
arch=('any')
url="https://launchpad.net/xubuntu-artwork"
license=('GPL')
depends=('xfce-theme-albatross' 'xfce-theme-bluebird' 'xfce-theme-greybird' 'shimmer-wallpapers')
optdepends=('plymouth: For plymouth theme to work'
        'lightdm-unity-greeter: For LightDM Unity Greeter theme to work'
	'elementary-xfce-icons: The matching icon theme, or if you want use the git version')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/xubuntu-artwork_${pkgver}.tar.gz"
        '0001_Plymouth_dir.patch')
sha512sums=('b872ee9f8280f46ec250191cba3510009cb50e9961149627f03eaba4933914f1f99ced812f06e05a1aa9c4eb7c69a740386b17811c6c21f7d223c2f97caabb45'
            '66c87fad65d51b5a030f153c21fdfba6657ab63afa005a0cdaaf6a55f7aba40ca59bb16cd907608a71fab3ae2fc510258f490d23231dcbc0a802988c5f581446')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -p1 -i "${srcdir}/0001_Plymouth_dir.patch"
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  install -dm755 "${pkgdir}/usr/"

  msg2 'Install plymouth theme'
  mkdir -p "${pkgdir}/usr/share/"
  cp -av lib/plymouth/ "${pkgdir}/usr/share/"

  msg2 'Install the rest of the files'
  cp -av usr/share/ "${pkgdir}/usr/"

  msg2 'Remove Elementary-Xfce bundle with this artwork, is outdated'
  rm -frv "${pkgdir}"/usr/share/icons

  msg2 'Move backdrops to a better place'
  mkdir -p "${pkgdir}/usr/share/backgrounds/xfce"
  mv -v "${pkgdir}"/usr/share/xfce4/backdrops/* "${pkgdir}"/usr/share/backgrounds/xfce
  rm -frv "${pkgdir}/usr/share/xfce4"
}

# vim:set ts=2 sw=2 et:
