# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=xubuntu-artwork
pkgver=13.10.2
pkgrel=1
pkgdesc="Xubuntu themes and artwork"
arch=('any')
url="https://launchpad.net/xubuntu-artwork"
license=('GPL')
depends=('xfce-theme-albatross' 'xfce-theme-bluebird' 'xfce-theme-greybird' 'shimmer-wallpapers')
optdepends=('plymouth: For plymouth theme to work'
            'lightdm-unity-greeter: For LightDM Unity Greeter theme to work')
provides=('elementary-xfce-icons')
conflicts=('elementary-xfce-icons')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/xubuntu-artwork_${pkgver}.tar.gz"
        '0001_Plymouth_dir.patch')
sha512sums=('6ab8b17235636c0153af37baef6e79df587179f9c48a1b8cae6a516bf704b490075b821f8ee5fe6e6aed942170e33fc6a81fc69c0cf9de2e5a45ed5305840850'
            '66c87fad65d51b5a030f153c21fdfba6657ab63afa005a0cdaaf6a55f7aba40ca59bb16cd907608a71fab3ae2fc510258f490d23231dcbc0a802988c5f581446')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -p1 -i "${srcdir}/0001_Plymouth_dir.patch"
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  install -dm755 "${pkgdir}/usr/"

  # Install plymouth theme
  mkdir -p "${pkgdir}/usr/share/"
  cp -a lib/plymouth/ "${pkgdir}/usr/share/"

  # Install the rest of the files
  cp -a usr/share/ "${pkgdir}/usr/"
}

# vim:set ts=2 sw=2 et:
