# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Contributor: hbdee <hbdee.arch@gmail.com>

pkgname=gtk2-appmenu
pkgver=2.24.20
pkgrel=1
pkgdesc="GTK+ is a multi-platform toolkit (v2) (Last version with appmenu patches)"
arch=('i686' 'x86_64')
url="http://www.gtk.org/"
license=('LGPL')
depends=('atk' 'pango' 'libxcursor' 'libxinerama' 'libxrandr' 'libxi' 'libxcomposite' 'libxdamage' 'shared-mime-info' 'libcups' 'gtk-update-icon-cache' 'python2')
makedepends=('pkg-config' 'gobject-introspection' 'gtk-doc')
provides=("gtk2=${pkgver}" "gtk2-ubuntu=${pkgver}")
conflicts=('gtk2' 'gtk2-ubuntu')
replaces=('gtk2-docs')
backup=('etc/gtk-2.0/gtkrc')
options=('!libtool')
install=arch_gtk2.install
source=("http://ftp.gnome.org/pub/gnome/sources/gtk+/2.24/gtk+-${pkgver}.tar.xz"
        "http://bazaar.launchpad.net/~ubuntu-branches/ubuntu/saucy/gtk+2.0/saucy/download/256/043_ubuntu_menu_prox-20100610182850-hu5h2hkvf20flmir-1/043_ubuntu_menu_proxy.patch"
        'arch_xid-collision-debug.patch'
        'fix-ubuntumenuproxy-build.patch')
sha512sums=('45ed8069b208a18a736a15bd91a744687603dc422df3cf0ca1e9128e13fb92ff1bd023f79efa32f4e30b98247d78021d76efa8e87dc7c141ba860cbce27f13d2'
            '28c4c6f5e289a9aded06f64e91707b56757e6629cfb827bdfc5aa297a34b8acaa103c94852c6851246837949f6af5952eb6be8bfad5f28eee5b8bec8ab5bf893'
            '89e3223c86731a76b12d39245f1ec4cf8b4860ab1d11d74a10e5deb0db302f20b4bb5d9034da1d8aeb96369dbdeddcdd2f28943270dc501476c694562b960159'
            '4b27afecb82e3517c2584665f8d2a2911193254a00b82ff8064caf4016ab029c76e99af502bb9271a66209594b50b2729797defcc9512d341aa61aff4f64ec24')

prepare() {
  cd "${srcdir}/gtk+-${pkgver}"

  patch -p1 -i "${srcdir}/arch_xid-collision-debug.patch"
  
  # legacy appmenu patch
  patch -p1 -i "${srcdir}/043_ubuntu_menu_proxy.patch"
  # Build fix from György Balló
  patch -p1 -i "${srcdir}/fix-ubuntumenuproxy-build.patch"
}

build() {
  cd "${srcdir}/gtk+-${pkgver}"

  autoreconf -vfi

  CXX=/bin/false ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --with-xinput=yes \
    --enable-test-print-backend

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd "${srcdir}/gtk+-${pkgver}"

  make DESTDIR="${pkgdir}" install
  sed -i "s#env python#env python2#" "${pkgdir}/usr/bin/gtk-builder-convert"
  echo 'gtk-fallback-icon-theme = "gnome"' > "${pkgdir}/etc/gtk-2.0/gtkrc"
  # Use the official gtk-update-icon-cache package
  rm -v "${pkgdir}/usr/bin/gtk-update-icon-cache"
}

# vim:set ts=2 sw=2 et:
