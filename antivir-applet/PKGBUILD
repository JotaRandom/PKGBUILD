# Maintainer: TDY <tdy@gmx.com>

pkgname=antivir-applet
pkgver=2.9
pkgrel=1
pkgdesc="Avira Antivir Guard Applet"
arch=('i686' 'x86_64')
url="http://www.free-av.com/"
license=('GPL')
depends=('antivir' 'dazukofs' 'gnome-panel')
makedepends=('pkg-config')
optdepends=('xfce4-xfapplet-plugin: for xfce4-panel support')
install=$pkgname.install
source=(http://dlpe.antivir.com/package/wks_avira/unix/en/pers/antivir_workstation-pers.tar.gz)
md5sums=('b38e442499f99c449569da35b49489b6')

build() {
  cd "$srcdir/antivir-workstation-pers-3.1.3.4-1/contrib/applet"
  make clean && make || return 1
}

package() {
  cd "$srcdir/antivir-workstation-pers-3.1.3.4-1/contrib/applet"
  install -dm755 "$pkgdir/usr/share/pixmaps"
  install -Dm755 avira-guard-applet \
    "$pkgdir/usr/lib/gnome-panel/avira-guard-applet"
  install -Dm644 data/AvGuardApplet.server \
    "$pkgdir/usr/lib/bonobo/servers/AvGuardApplet.server"
  install -Dm755 extra/popup_message.sh \
    "$pkgdir/usr/lib/AntiVir/popup_message.sh"
  install -m644 data/avira-guard-{icon,open,closed}.png \
    "$pkgdir/usr/share/pixmaps/"
  install -Dm644 data/gnome-panel-avira.png \
    "$pkgdir/usr/share/icons/hicolor/32x32/apps/gnome-panel-avira.png"
}
