# Maintainer: Cascendo <cascendo at hotmail dot com>
# Contributor: Aliaksandr Stelmachonak <mail.avatar at gmail dot com>

_name=gnome-globalmenu
pkgname=$_name-xfce4
pkgver=0.7.8
pkgrel=8
pkgdesc="Global Menu Bar built for using with Xfce desktop"
url="http://code.google.com/p/gnome2-globalmenu/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('xfce4-panel')
makedepends=('intltool')
options=('!libtool')
conflicts=('gtk2-aqd' 'gtk2-globalmenu' 'gnome-globalmenu-svn'
           'gnome-globalmenu' 'gnome-globalmenu-git')
install=$pkgname.install
source=("http://gnome2-globalmenu.googlecode.com/files/$_name-$pkgver.tar.bz2" "gnome-globalmenu-xfce4-Werror-fix.patch")
md5sums=('c2900eb3ff345457f65d9be2793cc2dc'
         '36b62f1636a66486e3e26987b2369668')

build() {
  cd "$srcdir/$_name-$pkgver"
  patch -Np1 -i $srcdir/gnome-globalmenu-xfce4-Werror-fix.patch

  # Does not build with --as-needed
  export LDFLAGS="${LDFLAGS//,--as-needed}"

  # fix xfce4 paths retrieving with pkg-config
  sed -e "s|\$.*libexec.*\(/xfce4/panel-plugins\)|/usr/lib\1|" \
      -e "s|\$.*datadir.*\(/xfce4/panel-plugins\)|/usr/share\1|" \
      -e "/GMENU/d" \
      -i configure.ac

  autoconf
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --libexecdir=/usr/lib \
              --without-gnome-panel \
              --with-xfce4-panel \
              --disable-tests
  make
}

package() {
  cd "$srcdir/$_name-$pkgver"
  make DESTDIR="$pkgdir/" GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 install
}
