# Maintainer: Lukas Martini <lutoma@ohai.su>
pkgname=prey-node-client
pkgver=1.3.6
pkgrel=0
pkgdesc="Remote tracking and monitoring application for laptops, smartphones, and other electronic devices"
url="https://preyproject.com/"
arch=('x86_64' 'i686')
license=('GPLv3')
depends=('nodejs' 'mpg123' 'xawtv' 'scrot' 'openssh')
replaces=('prey-tracker')
# Should be used as soon as the bash client is renamed
#provides=('prey-tracker')
install='prey-node-client.install'

# The URL uses x64 and x86 to identify architecture. While this is a weird
# format, we can just use the last two characters of $CARCH to choose one.
source=("https://s3.amazonaws.com/prey-releases/node-client/${pkgver}/prey-linux-${pkgver}-x${CARCH:(-2)}.zip")

[ "$CARCH" == "i686" ] && sha256sums=('556d49a3cd32b4f503c88d1004d9ea8995c598cc942bfabe459eaf03046d19be')
[ "$CARCH" == "x86_64" ] && sha256sums=('dbf964bf4e620dbb3247ce074213dfa461ed3c20ff1f3803a113550b5e449637')

package() {
  cd "${srcdir}/prey-${pkgver}"

  # We don't need the bundled node.js as we have it installed in the system.
  # Conveniently, the bash script probes for node.js and automatically uses
  # the system node if the bundled one is not found.
  rm bin/node

  # Disable automatic updates by default
  sed -i "s/auto_update = true/auto_update = false/" prey.conf.default

  # Fix the automatic symlink detection in the main script
  sed -i 's#dir="$dir/$rel"#dir="$rel"#' bin/prey
  sed -i 's/via npm/via pacman/' bin/prey

  mkdir -p "$pkgdir/opt/prey-node-client/"
  cp -r ./* "$pkgdir/opt/prey-node-client/"
  install -Dm644 license.txt "$pkgdir/usr/share/licenses/$pkgname/license.txt"

  mkdir -p "$pkgdir/usr/bin/"
  ln -s /opt/prey-node-client/bin/prey "$pkgdir/usr/bin/prey"
}

# vim:set ts=2 sw=2 et:
