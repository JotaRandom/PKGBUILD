# Maintainer : Jekyll Wu<adaptee [at] gmail [dot] com>
# Maintainer : osily <ly50247 at gmail dot com>
# Contributor: Lee.MaRS <leemars@gmail.com>

pkgname=qterm
pkgver=0.5.12
pkgrel=3
pkgdesc="BBS client for Linux, written in Qt"
depends=('qt4' 'openssl')
#optdepends=('phonon')
makedepends=('cmake')
url="http://www.qterm.org/wiki/index.php/Main_Page"
arch=('i686' 'x86_64')
license=('GPL')
source=(http://prdownloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.bz2
        0001-Compile-with-gcc-4.7-fix-bug-3474368.patch
        0002-Use-the-new-find_package-syntax-introduced-in-CMake-.patch
        )

build() {
    cd ${srcdir}/${pkgname}-${pkgver}
    patch -p1 -i ../0001-Compile-with-gcc-4.7-fix-bug-3474368.patch
    patch -p1 -i ../0002-Use-the-new-find_package-syntax-introduced-in-CMake-.patch

    if [ -d build ] ; then
        rm build -rf
    fi

    mkdir build && cd build

    # disable the phonon suppot until someones figure out how to make it build
    cmake -DCMAKE_INSTALL_PREFIX=/usr -DQT_QMAKE_EXECUTABLE=qmake-qt4 -DQTERM_ENABLE_PHONON=OFF ../
    make
}

package()
{
    cd ${srcdir}/${pkgname}-${pkgver}/build
    make DESTDIR=${pkgdir} install
}
md5sums=('a151e1b14bc5ebc70e67d49f40b5332c'
         'f905cea498de35578d4dbdd96f043c76'
         '97abe4f7f1df512838d48cb593888204')
