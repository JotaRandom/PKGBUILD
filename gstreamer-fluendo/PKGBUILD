# Maintainer: Pablo Lezaeta <prflr88 (arroba) gmail (dot) com<
# Contributor: peace4all <markspost at rocketmail dot com>
# Contributor: jase

pkgbase=gstreamer-fluendo
pkgname=gstreamer0.10-fluendo
true & pkgname=('gstreamer0.10-fluendo' 'gst-fluendo')
pkgver=0.10.72
pkgrel=1
pkgdesc="GStreamer fluendo mpegdemux plugin"
arch=('i686' 'x86_64')
license=('MPL')
makedepends=('pkgconfig')
url="http://www.fluendo.com/products.php?product=plugins"
groups=('gstreamer0.10-plugins')
source=(http://core.fluendo.com/gstreamer/src/gst-fluendo-mpegdemux/gst-fluendo-mpegdemux-${pkgver}.tar.bz2)
md5sums=('df726579404af65b9536428661ab4322')

prepare(){
  cd $srcdir

  cp -rv gst-fluendo-mpegdemux-$pkgver gst-fluendo-mpegdemux-0.10
  cp -rv gst-fluendo-mpegdemux-$pkgver gst-fluendo-mpegdemux-1.00
}

build() {
# build for gst0.10
  cd $srcdir/gst-fluendo-mpegdemux-0.10

  sed -i -e "s|GST_VER_1_0|GST_VER_0_0|" \
  -e "s|gstreamer-1.0 >= 1.0.0|gstreamer-0.0 >= 0.0.0|" \
  $(find . -name 'configure')

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
	--libexecdir=/usr/lib/gstreamer-0.10 --sbindir=/usr/bin --bindir=/usr/bin \
	--with-gstreamer-api=0.10
  make

# build for gst1.0
  cd $srcdir/gst-fluendo-mpegdemux-1.00

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
	--libexecdir=/usr/lib/gstreamer-1.0 --sbindir=/usr/bin --bindir=/usr/bin \
	--with-gstreamer-api=1.0
  make
}

package_gstreamer0.10-fluendo() {
depends=('gstreamer0.10-base')

  cd $srcdir/gst-fluendo-mpegdemux-0.10
  make DESTDIR="$pkgdir/" install sbindir=/usr/bin
}

package_gst-fluendo() {
depends=('gstreamer0.10-base')

  cd $srcdir/gst-fluendo-mpegdemux-1.00
  make DESTDIR="$pkgdir/" install sbindir=/usr/bin
}

