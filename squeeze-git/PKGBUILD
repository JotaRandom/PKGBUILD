# $Id$
# Contributor: Jan Jezek <honzin.jezek@gmail.com>
# Contributor: Skunnyk <skunnyk@archlinux.fr>
# Maintainer : Pablo Lezaeta <prflr88@gmail.com>

pkgname=squeeze-git
realname=squeeze
pkgver=0.2.0.git958
pkgrel=1
epoch=1
pkgdesc="Squeeze is  a modern and advanced archive manager for the Xfce Desktop Environment"
arch=(i686 x86_64)
license=('GPL2')
url="http://www.xfce.org/"
groups=('xfce4-git')
makedepends=('git' 'xfce4-dev-tools' 'pkgconfig' 'gtk-doc' 'intltool')
depends=('libxfce4util' 'hicolor-icon-theme' 'dbus-glib' 'gtk2' 'desktop-file-utils')
conflicts=('squeeze')
provides=("squeeze" "squeeze-git")
options=('!libtool')
install=squeeze.install
branch=("stephan/new-ui")
source=("$realname::git+git://git.xfce.org/apps/squeeze#branch=$branch")
md5sums=('SKIP')

pkgver(){
	cd $realname
	echo "$(git describe --always --abbrev=0 | sed 's/squeeze-//g' ).git$(git rev-list --count HEAD | sed 's/-/./g' )"
}

build(){
	cd $srcdir/$realname
	./autogen.sh --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib/$realname \
	--localstatedir=/var --disable-debug --bindir=/usr/bin --sbindir=/usr/bin \
	--libdir=/usr/lib 
  	make
}

package() {
	cd $srcdir/$realname
	make DESTDIR=$pkgdir sbindir=/usr/bin install
}

