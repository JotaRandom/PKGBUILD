# Maintainer: Claudio Kozicky <claudiokozicky@gmail.com>
# Contributor: Yann Kaiser <kaiser.yann@gmail.com>
# Contributor: mib1982 <michael.bentlage@web.de>

pkgname=cavestory+
pkgver=1355961600
pkgrel=4
pkgdesc="A platform-adventure game set in a cave"
arch=(i686 x86_64)
url="http://nicalis.com/games.php?game=cavestory"
license=(custom)
depends=(glu sdl)
options=(!strip)
source=(hib://cave_story_plus-linux-r100.tar.bz2
        $pkgname.sh
        $pkgname.desktop
        $pkgname.png)
md5sums=(b7ecd65644b8607bc177d7ce670f2185
         d6995b631554fe1ddc65119c9448dda1
         71928243ca1ee88ac956cdf9d53b3738
         2cc33b1b91383109cf1576a28215cdf7)
DLAGENTS+=('hib::/usr/bin/echo Could not find %u. Manually download it to \"$(pwd)\", or set up a hib:// DLAGENT in /etc/makepkg.conf.; exit 1')
PKGEXT=.pkg.tar

package()
{
    cd "$srcdir"

    # data
    find CaveStoryPlus -name icon.bmp -prune \
        -o -name "CaveStory+*" -prune \
        -o -name "lib*" -prune \
        -o -type f -exec install -Dm644 {} "$pkgdir"/opt/{} \;
    [ $CARCH = i686 ] \
        && install -m755 CaveStoryPlus/CaveStory+ "$pkgdir"/opt/CaveStoryPlus/CaveStory+
    [ $CARCH = x86_64 ] \
        && install -m755 CaveStoryPlus/CaveStory+_64 "$pkgdir"/opt/CaveStoryPlus/CaveStory+

    # launcher
    install -Dm755 $pkgname.sh "$pkgdir"/usr/bin/$pkgname

    # desktop integration
    install -Dm644 CaveStoryPlus/data/icon.bmp "$pkgdir"/usr/share/pixmaps/$pkgname.bmp
    install -Dm644 $pkgname.png "$pkgdir"/usr/share/pixmaps/$pkgname.png
    install -Dm644 $pkgname.desktop "$pkgdir"/usr/share/applications/$pkgname.desktop
}

# vim:et:sw=4:sts=4
