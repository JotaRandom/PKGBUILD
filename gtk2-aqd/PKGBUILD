#Originally by AqD
#Last edited by - Victor Aurélio <victoraur.santos@gmail.com>
# Maintainer: Victor Aurélio <victoraur.santos@gmail.com>
# Contributor: Jristz <prflr88@gmail.com>

pkgname=gtk2-aqd
pkgver=2.24.20
pkgrel=1
pkgdesc="The GTK+ Toolkit (v2), with mac-style menubar"
arch=(i686 x86_64)
url="http://www.gtk.org/"
install=gtk2.install
depends=('atk' 'pango' 'libxcursor' 'libxinerama' 'libxrandr' 'libxi' 'libxcomposite' 'libxdamage'
         'shared-mime-info' 'cairo' 'libcups' 'gtk-update-icon-cache')
makedepends=('atk' 'pango' 'libxcursor' 'libxinerama' 'libxrandr' 'libxi' 'libxcomposite' 'libxdamage'
             'shared-mime-info' 'cairo' 'libcups' 'gdk-pixbuf2' 'gobject-introspection')
provides=("gtk2=$pkgver")
replaces=('gtk2' 'gtk2-docs')
conflicts=('gtk2')
options=('!libtool')
backup=(etc/gtk-2.0/gtkrc)
license=(LGPL)
source=("http://ftp.gnome.org/pub/GNOME/sources/gtk+/2.24/gtk+-$pkgver.tar.xz"
        xid-collision-debug.patch
        gtk-gtkmenubar.patch)
backup=(etc/gtk-2.0/gtkrc)

prepare() {
    cd $srcdir/gtk+-$pkgver
    ## Apply Patches
    patch -Np0 < $srcdir/xid-collision-debug.patch
    patch -Np0 < $srcdir/gtk-gtkmenubar.patch
}

build(){
    cd $srcdir/gtk+-$pkgver
    CXX=/bin/false ./configure --prefix=/usr --bindir=/usr/bin \
                                --sysconfdir=/etc --sbindir=/usr/bin \
                                --localstatedir=/var --libdir=/usr/lib \
                                --with-xinput=yes --libexecdir=/usr/lib/gtk-2.0 

    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool                                
    make
}

package() {
    cd $srcdir/gtk+-$pkgver

    make DESTDIR="$pkgdir" sbindir=/usr/bin install
    sed -i "s#env python#env python2#" $pkgdir/usr/bin/gtk-builder-convert
    echo 'gtk-fallback-icon-theme = "gnome"' > "$pkgdir/etc/gtk-2.0/gtkrc"

    #split this out to use with gtk3 too
    rm $pkgdir/usr/bin/gtk-update-icon-cache
}

md5sums=('9d7833331d7accd80668e29d7f567ce5'
         '6c39627284d656fc70fae7c557ddc718'
         'f684703385e424343faeceb560e1fe02')
